# Linq를 디스어셈블리 해보자

MS Docs에서 [Linq](https://docs.microsoft.com/ko-kr/dotnet/csharp/programming-guide/concepts/linq/introduction-to-linq-queries)를 소개하는 글을 먼저 읽고오자

MS Docs에서 설명하는 쿼리의 세 파트, 지연된 실행을 자세히 확인할려고 한다.

이전의 Lambda글처럼 IL DASM 디스어셈블리를 이용한다.

우리는 Linq가 실행한 결과를 반환하지 않고, 데이터 소스의 IEnumerable을 반환할 뿐이라는 것을 확인할 것이다.



# 쿼리의 지연된 실행


MS Docs의 예제로는 부족한 감이 있다.
지연된 실행을 더 쉽게 볼수 있는 예제 코드는 다음과 같다.

    static void Main(string[] args)
    {
        List<Int32> numlist = new List<Int32> { 1,2,3,4,5,6,7,8,9};

        IEnumerable<Int32> query = from num in numlist where (num % 2) == 0 select num;

        foreach(var num in query)
        {
            Console.WriteLine(num);
        }

        numlist.Add(10);

        foreach(var num in query)
        {
            Console.WriteLine(num);
        }
    }

이 코드의 실행 결과는 다음과 같다

    2
    4
    6
    8
    2
    4
    6
    8
    10

우리는 같은 쿼리의 실행 결과가 다른 것을 확인할 수 있다. 이는 우리가 쿼리를 생성할 때가 아닌 실행할 떄에 결과가 반환된다는 것을 알 수 있다.

이 예제를 디스어셈블리해서 더 자세하게 살펴보자


    .method private hidebysig static void  Main(string[] args) cil managed
    {
        .entrypoint
        // 코드 크기       266 (0x10a)
        .maxstack  3
        .locals init ([0] class [mscorlib]System.Collections.Generic.List`1<int32> numlist,
                [1] class [mscorlib]System.Collections.Generic.IEnumerable`1<int32> query,
                [2] int32 num,
                [3] class [mscorlib]System.Collections.Generic.List`1<int32> '<>g__initLocal0',
                [4] class [mscorlib]System.Collections.Generic.IEnumerator`1<int32> CS$5$0000,
                [5] bool CS$4$0001)
        /* list 생성 및 요소 추가 (1~9까지) */ 
        IL_0000:  nop
        IL_0001:  newobj     instance void class [mscorlib]System.Collections.Generic.List`1<int32>::.ctor()
        //...
        //list에 1~9까지 삽입
        //...
        /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
        /* 정적필드인 9__CachedAnonymousMethodDelegate2를 스택 메모리로 로드하여 <Main>b__1을 가리키는 Func'2를 생성하고 Func'2를 9__CachedAnonymousMethodDelegate2에 저장 */
        IL_004f:  nop
        IL_0050:  ldloc.3
        IL_0051:  stloc.0
        IL_0052:  ldloc.0
        IL_005b:  ldsfld     class [mscorlib]System.Func`2<int32,bool> ConsoleApplication1.Program::'CS$<>9__CachedAnonymousMethodDelegate2'
        IL_0060:  brtrue.s   IL_0075
        IL_0062:  ldnull
        IL_0063:  ldftn      bool ConsoleApplication1.Program::'<Main>b__1'(int32)
        IL_0069:  newobj     instance void class [mscorlib]System.Func`2<int32,bool>::.ctor(object,
                                                                                            native int)
        IL_006e:  stsfld     class [mscorlib]System.Func`2<int32,bool> ConsoleApplication1.Program::'CS$<>9__CachedAnonymousMethodDelegate2'
        IL_0073:  br.s       IL_0075
        IL_0075:  ldsfld     class [mscorlib]System.Func`2<int32,bool> ConsoleApplication1.Program::'CS$<>9__CachedAnonymousMethodDelegate2'
        /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
        /* Where 함수를 호출, 매개변수는 list와 9__CachedAnonymousMethodDelegate2를 참조한다. Where의 반환 값으로 IEnumerable를 획득 */
        IL_007a:  call       class [mscorlib]System.Collections.Generic.IEnumerable`1<!!0> [System.Core]System.Linq.Enumerable::Where<int32>(class [mscorlib]System.Collections.Generic.  IEnumerable`1<!!0>, class [mscorlib]System.Func`2<!!0,bool>)
        IL_007f:  stloc.1
        IL_0080:  nop
        /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
        /* GetEnumerator()가 반환한 IEnumerable를 스택에 푸쉬하여 try안에서 get_Current와 MoveNext를 반복하면서 foreach 안의 내용을 실행 */
        IL_0081:  ldloc.1
        IL_0082:  callvirt   instance class [mscorlib]System.Collections.Generic.IEnumerator`1<!0> class [mscorlib]System.Collections.Generic.IEnumerable`1<int32>::GetEnumerator()
        IL_0087:  stloc.s    CS$5$0000
        .try
        {
            IL_0089:  br.s       IL_009c
            IL_008b:  ldloc.s    CS$5$0000
            IL_008d:  callvirt   instance !0 class [mscorlib]System.Collections.Generic.IEnumerator`1<int32>::get_Current()
            IL_0092:  stloc.2
            IL_0093:  nop
            IL_0094:  ldloc.2
            IL_0095:  call       void [mscorlib]System.Console::WriteLine(int32)
            IL_009a:  nop
            IL_009b:  nop
            IL_009c:  ldloc.s    CS$5$0000
            IL_009e:  callvirt   instance bool [mscorlib]System.Collections.IEnumerator::MoveNext()
            IL_00a3:  stloc.s    CS$4$0001
            IL_00a5:  ldloc.s    CS$4$0001
            IL_00a7:  brtrue.s   IL_008b
            IL_00a9:  leave.s    IL_00bf
        }  // end .try
        finally
        {
            IL_00ab:  ldloc.s    CS$5$0000
            IL_00ad:  ldnull
            IL_00ae:  ceq
            IL_00b0:  stloc.s    CS$4$0001
            IL_00b2:  ldloc.s    CS$4$0001
            IL_00b4:  brtrue.s   IL_00be
            IL_00b6:  ldloc.s    CS$5$0000
            IL_00b8:  callvirt   instance void [mscorlib]System.IDisposable::Dispose()
            IL_00bd:  nop
            IL_00be:  endfinally
        }  // end handler
        IL_00bf:  nop
        /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
        /*  list에 10을 추가 */
        IL_00c0:  ldloc.0
        IL_00c1:  ldc.i4.s   10
        IL_00c3:  callvirt   instance void class [mscorlib]System.Collections.Generic.List`1<int32>::Add(!0)
        IL_00c8:  nop
        /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
        /* GetEnumerator()가 반환한 IEnumerable를 스택에 푸쉬하여 try안에서 get_Current와 MoveNext를 반복하면서 foreach 안의 내용을 실행 */
        IL_00c9:  nop
        IL_00ca:  ldloc.1
        IL_00cb:  callvirt   instance class [mscorlib]System.Collections.Generic.IEnumerator`1<!0> class [mscorlib]System.Collections.Generic.IEnumerable`1<int32>::GetEnumerator()
        IL_00d0:  stloc.s    CS$5$0000
        .try
        {
            IL_00d2:  br.s       IL_00e5
            IL_00d4:  ldloc.s    CS$5$0000
            IL_00d6:  callvirt   instance !0 class [mscorlib]System.Collections.Generic.IEnumerator`1<int32>::get_Current()
            IL_00db:  stloc.2
            IL_00dc:  nop
            IL_00dd:  ldloc.2
            IL_00de:  call       void [mscorlib]System.Console::WriteLine(int32)
            IL_00e3:  nop
            IL_00e4:  nop
            IL_00e5:  ldloc.s    CS$5$0000
            IL_00e7:  callvirt   instance bool [mscorlib]System.Collections.IEnumerator::MoveNext()
            IL_00ec:  stloc.s    CS$4$0001
            IL_00ee:  ldloc.s    CS$4$0001
            IL_00f0:  brtrue.s   IL_00d4
            IL_00f2:  leave.s    IL_0108
        }  // end .try
        finally
        {
            IL_00f4:  ldloc.s    CS$5$0000
            IL_00f6:  ldnull
            IL_00f7:  ceq
            IL_00f9:  stloc.s    CS$4$0001
            IL_00fb:  ldloc.s    CS$4$0001
            IL_00fd:  brtrue.s   IL_0107
            IL_00ff:  ldloc.s    CS$5$0000
            IL_0101:  callvirt   instance void [mscorlib]System.IDisposable::Dispose()
            IL_0106:  nop
            IL_0107:  endfinally
        }  // end handler
        IL_0108:  nop
        IL_0109:  ret
    } // end of method Program::Main


이번엔 코드가 길지만, 구분을 짓지면 다음과 같다.  

1. list 생성 및 요소 추가 (1~9까지)
2. 정적필드인 9__CachedAnonymousMethodDelegate2를 스택 메모리로 로드하여 Main>b__1을 가리키는 Func'2를 생성하고 Func'2를 __CachedAnonymousMethodDelegate2에 저장
3. Where 함수를 호출, 매개변수는 list와 9__CachedAnonymousMethodDelegate2를 조한다. Where의 반환 값으로 IEnumerable를 획득
4. GetEnumerator()가 반환한 IEnumerable를 스택에 푸쉬하여 try안에서 get_Current와 oveNext를 반복하면서 foreach 안의 내용을 실행
5. list에 10을 추가
6. 4의 내용을 다시 실행
  

<Main>b__1은 컴파일러가 Main 함수를 정의한 Program 클래스 내부에 정의했다.  
<Main>b__1은 우리가 Where에 익명 함수로 정의한 함수가 정의되어있다.


    .method private hidebysig static bool  '<Main>b__1'(int32 num) cil managed
    {
        .custom instance void [mscorlib]System.Runtime.CompilerServices.ompilerGeneratedAttribute::.ctor() = ( 01 00 00 00 ) 
        // 코드 크기       11 (0xb)
        .maxstack  2
        .locals init ([0] bool CS$1$0000)
        IL_0000:  ldarg.0
        IL_0001:  ldc.i4.2
        IL_0002:  rem
        IL_0003:  ldc.i4.0
        IL_0004:  ceq
        IL_0006:  stloc.0
        IL_0007:  br.s       IL_0009

        IL_0009:  ldloc.0
        IL_000a:  ret
    } // end of method Program::'<Main>b__1'


코드 하나하나에 대해서 설명하려면 스택과 정적, 로컬 메모리에 대해 설명이 필요한데, 이번 내용과 상관이 없으니 하지않겠다.

우리는 이를 통해 다섯가지 사실을 알 수 있다.

1. 컴파일라가 쿼리문을 Where 함수를 호출하도록 바꾼다.
2. Where 함수의 매개변수로 list와 delegate를 넘겨준다.
3. Where 함수는 IEnumable를 반환한다.
4. 반환된 IEnumable의 get_current, MoveNext 함수를 통하여 검색된 값을 확인한다.
5. 10을 추가한 이후로 IEnumable를 재생성하지 않는다.

안타깝게도 이것만으론 실행 시점에따라 결과가 다른 이유는 알기 어렵다. 다만 힌트는 얻을 수 있는데, Where이 반환하는 IEnumable이 중요한 역할을 한다는 것이다.

다행히 MS는 C#의 코드 대부분을 오픈했다. 
[MS Reference Source](https://referencesource.microsoft.com/) 에서 우리는 쉽게 C# 내부 코드를 확인할 수 있다.
여기서 우리는 Where 코드를 확인할 것이다.


## Linq의 IEnumable

MS Reference Source 에서 Where을 검색하면 다음과 같은 코드를 볼 수 있다.

    public static partial class Enumerable
    {
        public static IEnumerable<TSource> Where<TSource>(this IEnumerable<TSource> source, Func<TSource, bool> predicate) {
            if (source == null) throw Error.ArgumentNull("source");
            if (predicate == null) throw Error.ArgumentNull("predicate");
            if (source is Iterator<TSource>) return ((Iterator<TSource>)source).Where(predicate);
            if (source is TSource[]) return new WhereArrayIterator<TSource>((TSource[])source, predicate);
            if (source is List<TSource>) return new WhereListIterator<TSource>((List<TSource>)source, predicate);
            return new WhereEnumerableIterator<TSource>(source, predicate);
        }

        ///....
    }   


확장 메서드 Where을 확인할 수 있다. 그리고 내부를 보면 source의 타입에따라 반환되는 타입이 다르다. 각 타입에 따라서 WhereArrayIterator, WhereListIterator, WhereEnumerableIterator을 생성하여 반환한다.
이 클래스들은 공통적으로 Iterator을 상속받고,  Iterator는 IEnumerable, IEnumerator을 상속받기에 반환될 수 있다.
이중 우리는 WhereListIterator를 살펴보자.
WhereListIterator는 다음과 같다.

    class WhereListIterator<TSource> : Iterator<TSource>
    {
        List<TSource> source;
        Func<TSource, bool> predicate;
        List<TSource>.Enumerator enumerator;
 
        public WhereListIterator(List<TSource> source, Func<TSource, bool> predicate) {
            this.source = source;
            this.predicate = predicate;
        }
 
        public override Iterator<TSource> Clone() {
            return new WhereListIterator<TSource>(source, predicate);
        }
 
        public override bool MoveNext() {
            switch (state) {
                case 1:
                    enumerator = source.GetEnumerator();
                    state = 2;
                    goto case 2;
                case 2:
                    while (enumerator.MoveNext()) {
                        TSource item = enumerator.Current;
                        if (predicate(item)) {
                            current = item;
                            return true;
                        }
                    }
                    Dispose();
                    break;
            }
            return false;
        }
 
        public override IEnumerable<TResult> Select<TResult>(Func<TSource, TResult> selector) {
            return new WhereSelectListIterator<TSource, TResult>(source, predicate, selector);
        }
 
        public override IEnumerable<TSource> Where(Func<TSource, bool> predicate) {
            return new WhereListIterator<TSource>(source, CombinePredicates(this.predicate, predicate));
        }
    }


WhereListIterator의 생성자에 주목하자. 매개변수인 List와 Func2를 그대로 멤버변수에 대입한다.

예제코드의 경우 source는 numlist이고, predicate는 익명함수로 만든 (num % 2) == 0이 될 것이다. (디스어셈블리에선 b__1 함수)

source에서 주의할 점은 복사를 하지않고, 리스트를 참조했다는 것이다. 참조를 한다는 것은 원본이 변경되면, 리스트를 참조하고 있는 source의 결과또한 바뀐다는 것이다.

그럼 predicate는 언제 호출할까? predicate를 호출해야 우리가 원하는 조건대로 필터링이 될텐데 말이다.

그것은 MoveNext에서 실행한다.
MoveNext를 호출해서, 다음 요소가 true인 값이 current가 된다는 것을 볼 수 있다.

디스어셈블리에서 IL_0081(또는 IL_00ca)부터 IL_00a1(또는 IL_00ea)에서 get_Current를 통해 현재 요소를 받으면서 MoveNext를 돌리면서 true가 되는 값만 콘솔창에 출력하는 코드를 볼 수 있다.

    .try
    {
        IL_0089:  br.s       IL_009c
        IL_008b:  ldloc.s    CS$5$0000
        IL_008d:  callvirt   instance !0 class [mscorlib]System.Collections.Generic.IEnumerator`1<int32>::get_Current()
        IL_0092:  stloc.2
        IL_0093:  nop
        IL_0094:  ldloc.2
        IL_0095:  call       void [mscorlib]System.Console::WriteLine(int32)
        IL_009a:  nop
        IL_009b:  nop
        IL_009c:  ldloc.s    CS$5$0000
        IL_009e:  callvirt   instance bool [mscorlib]System.Collections.IEnumerator::MoveNext()
        IL_00a3:  stloc.s    CS$4$0001
        IL_00a5:  ldloc.s    CS$4$0001
        IL_00a7:  brtrue.s   IL_008b
        IL_00a9:  leave.s    IL_00bf
    }  // end .try
    finally
    {
        IL_00ab:  ldloc.s    CS$5$0000
        IL_00ad:  ldnull
        IL_00ae:  ceq
        IL_00b0:  stloc.s    CS$4$0001
        IL_00b2:  ldloc.s    CS$4$0001
        IL_00b4:  brtrue.s   IL_00be
        IL_00b6:  ldloc.s    CS$5$0000
        IL_00b8:  callvirt   instance void [mscorlib]System.IDisposable::Dispose()
        IL_00bd:  nop
        IL_00be:  endfinally
    }  // end handler
